apply plugin: 'com.android.library'
apply plugin: 'com.bmuschko.nexus'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 2
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = "net.kianoni"
version = "0.4.0"

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

modifyPom {
    project {
        name 'Android Font Loading Textview'
        description 'Helper for using custom fonts in styles'
        url 'https://github.com/Kianoni/android-fontloading-textview'

        scm {
            url 'https://github.com/Kianoni/android-fontloading-textview'
            connection 'scm:https://github.com/Kianoni/android-fontloading-textview.git'
            developerConnection 'scm:https://github.com/Kianoni/android-fontloading-textview.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kianoni'
                name 'Kimmo Ilppola'
                email 'kimmo.ilppola@futurice.com'
            }
        }
    }
}